include_guard(GLOBAL)

function(target_add_sanitizer target)
    set(libraries ${ARGN})
    if (CMAKE_SYSTEM_NAME STREQUAL "Windows")
        if (CMAKE_CXX_COMPILER_ID STREQUAL "MSVC")
            set_property(TARGET ${target} ${libraries} APPEND PROPERTY COMPILE_OPTIONS /fsanitize=address)
            target_link_options(${target} PRIVATE /INCREMENTAL:NO)
            set_target_properties(${target} PROPERTIES
                MSVC_DEBUG_INFORMATION_FORMAT "$<IF:$<AND:$<C_COMPILER_ID:MSVC>,$<CXX_COMPILER_ID:MSVC>>,$<$<CONFIG:Debug,RelWithDebInfo>:EditAndContinue>,$<$<CONFIG:Debug,RelWithDebInfo>:ProgramDatabase>>"
            )
            return()
        elseif(CMAKE_CXX_COMPILER_ID MATCHES "Clang")
            if (CMAKE_BUILD_TYPE STREQUAL "Debug")
                message(WARNING "The debug build type is not supported for Windows Clang sanitizers")
            endif()
            set_property(TARGET ${target} ${libraries} APPEND PROPERTY COMPILE_OPTIONS -fsanitize=address,undefined)
            set_property(TARGET ${target} ${libraries} APPEND PROPERTY COMPILE_DEFINITIONS _DISABLE_STRING_ANNOTATION _DISABLE_VECTOR_ANNOTATION)
            target_link_options(${target} PRIVATE -coverage -fsanitize=address,undefined)
            set_target_properties(${target} ${libraries} PROPERTIES MSVC_RUNTIME_LIBRARY "MultiThreaded")
            return()
        endif()
    endif()
    target_compile_options(${target} PRIVATE -fsanitize=address,undefined)
    target_link_options(${target} PRIVATE -coverage -fsanitize=address,undefined)
endfunction()
